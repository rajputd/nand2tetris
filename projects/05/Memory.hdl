// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load==1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the 
 * Screen and Keyboard chip specifications given in the book.
 */

// 0b000000000000000 = RAM START = 0
// 0b011111111111111 = RAM END = 16383
// 0b100000000000000 = SCREEN START = 16384
// 0b101111111111111 = SCREEN END = 24575
// 0b110000000000000 = Keyboard

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    //helpers
    Or(a=address[14], b=false, out=MSBSet);
    Or(a=address[13], b=false, out=secondMSBSet);
    Not(in=secondMSBSet, out=secondMSBNotSet);

    //select which one to select
    Not(in=address[14], out=selectRam);
    And(a=MSBSet, b=secondMSBNotSet, out=selectScreen);

    //decide when to load
    And(a=selectRam, b=load, out=loadRam);
    And(a=selectScreen, b=load, out=loadScreen);
    
    RAM16K(in=in, load=loadRam, address=address[0..13], out=ramOut);
    Screen(in=in, load=loadScreen, address=address[0..12], out=screenOut);
    Keyboard(out=kbdOut);

    Mux4Way16(a=ramOut, b=ramOut, c=screenOut, d=kbdOut, sel=address[13..14], out=out);

}